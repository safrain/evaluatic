def springVersion = "2.5.6.SEC03"
def servletVersion = "2.5"
def groovyVersion = "1.8.9"
def slf4jVersion = "1.6.4"

allprojects {
    apply plugin: "idea"
    apply plugin: "maven"
    apply plugin: "signing"
    group = "com.github.safrain"
    version = "1.0-SNAPSHOT"

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "eclipse"
    configurations {
        provided
    }

    idea.module {
        scopes.PROVIDED.plus += configurations.provided
    }

    sourceSets {
        main { compileClasspath += configurations.provided }
    }
}

project(":evaluatic-core") {
    dependencies {
        compile "javax.servlet:servlet-api:${servletVersion}"
        compile "org.springframework:spring:${springVersion}"
        compile "mysql:mysql-connector-java:5.1.18"
        testCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    }

    signing {
        sign configurations.archives
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = "javadoc"
        from "build/docs/javadoc"
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = "sources"
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { signing.signPom(it) }

                repository(url:
                        version.endsWith("SNAPSHOT") ?
                            "https://oss.sonatype.org/content/repositories/snapshots/" : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                ) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    description ""
                    url "http://safrain.github.io/evaluatic/"
                    licenses {
                        license {
                            name "WTFPL v2"
                            url "http://www.wtfpl.net"
                            distribution "repo"
                        }
                    }
                    scm {
                        url "http://safrain.github.io/evaluatic/"
                        connection "scm:git:git@github.com:safrain/evaluatic.git"
                        developerConnection "scm:git:git@github.com:safrain/evaluatic.git"
                    }
                    developers {
                        developer {
                            id "safrain"
                            name "safrain"
                            email "z.safrain@gmail.com"
                        }
                    }
                }
            }
        }
    }
}
project(":evaluatic-groovy") {
    dependencies {
        provided "org.codehaus.groovy:groovy-all:${groovyVersion}"
        provided "org.apache.velocity:velocity:1.7"
        provided "org.apache.velocity:velocity-tools:2.0"
        provided "org.slf4j:slf4j-api:${slf4jVersion}"
        compile project(":evaluatic-core")
    }
}
project(":evaluatic-example") {
    apply plugin: "war"
    apply plugin: "jetty"
    [jettyRun, jettyRunWar].each {
        it.contextPath = "/"
    }
    dependencies {
        compile "javax.servlet:servlet-api:${servletVersion}"
        compile "mysql:mysql-connector-java:5.1.18"
        compile "org.springframework:spring:${springVersion}"
        compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
        compile "org.apache.velocity:velocity:1.7"
        compile "org.apache.velocity:velocity-tools:2.0"
        compile "org.slf4j:slf4j-api:${slf4jVersion}"
        compile "log4j:log4j:1.2.17"
        compile "commons-dbcp:commons-dbcp:1.2.2"
        compile project(":evaluatic-core")
        compile project(":evaluatic-groovy")
    }
}
